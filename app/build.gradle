apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        applicationId "com.handstandsam.shoppingapp"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    dexOptions {
        jumboMode true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }
}

dependencies {

    implementation project(':models')
    implementation project(":mock-data")

    //Support Libraries
    implementation Libs.supportV4
    implementation Libs.support_appcompat_v7
    implementation Libs.supportRecyclerView
    implementation Libs.supportDesign
    //Rx
    implementation Libs.rxjava2
    implementation Libs.rx_android
    implementation Libs.retrofit_rxjava_adapter
    //Networking
    implementation Libs.glide
    implementation Libs.retrofit
    implementation Libs.retrofit_moshi_converter
    implementation Libs.okHttp
    implementation Libs.dagger
    kapt Libs.daggerKapt
    //Logging
    implementation Libs.timber
    //DEBUG ONLY
    debugImplementation Libs.stetho
    debugImplementation Libs.stethoOkHttp3
    debugImplementation Libs.okHttp3LoggingInterceptor
    debugImplementation Libs.processPhoenix
    debugImplementation Libs.chuck

    //JVM Tests Only
    testImplementation Libs.junit

    //WireMock - It's typically bad practice to put WireMock in a release because of it's large size, but this is a sample app.
    implementation(Libs.wiremock) {
        //Using Android Version Instead
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'

        //Was getting a classpath conflict for org.objectweb.asm.AnnotationVisitor which is a part of 'net.minidev:asm'
        exclude group: 'org.ow2.asm', module: 'asm'

        //Was getting this warning, so decided to ignore this version included by WireMock.
        //Warning:Dependency org.json:json:20090211 is ignored as it may be conflicting with the internal version provided by Android.
        //In case of problem, please repackage with jarjar to change the class packages
        exclude group: 'org.json', module: 'json'
    }
    implementation Libs.apacheHttpClientAndroid
    implementation 'commons-io:commons-io:2.4'
    androidTestImplementation Libs.espressoCore
    androidTestImplementation Libs.espressoContrib
    androidTestImplementation Libs.runner
    androidTestUtil Libs.orchestrator
    implementation Libs.kotlinStdLib
}
repositories {
    mavenCentral()
}
