apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.build_tools
    defaultConfig {
        applicationId "com.handstandsam.shoppingapp"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    dexOptions {
        jumboMode true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    flavorDimensions "server"

    productFlavors {
        inmemory {
            dimension "server"
        }

        mockserver {
            dimension "server"
        }

        liveserver {
            dimension "server"
        }
    }

    variantFilter { variant ->
        def fullName = variant.name.toLowerCase()
        def names = variant.flavors*.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (fullName.endsWith("debug")) {
            println(fullName + " - " + names)
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(false)
        } else {
            setIgnore(true)
        }
    }


}

dependencies {

    implementation project(Modules.models)
    implementation project(Modules.mockData)

    //Support Libraries
    implementation Libs.support_v4
    implementation Libs.support_appcompat_v7
    implementation Libs.support_recycler_view
    implementation Libs.support_design

    //Networking
    implementation Libs.glide
    implementation Libs.retrofit
    implementation Libs.okhttp

    //Kotlin
    implementation Libs.kotlin_std_lib
    implementation Libs.kotlin_coroutines
    implementation Libs.kotlin_coroutines_android

    implementation Libs.moshi

    //Flavor Modules
    inmemoryImplementation project(Modules.flavorInmemory)
    mockserverImplementation project(Modules.flavorMockserver)
    liveserverImplementation project(Modules.flavorLiveserver)

    //Logging
    implementation Libs.timber

    debugImplementation project(Modules.debug)


    implementation project(Modules.networking)

    //JVM Tests Only
    testImplementation TestLibs.junit

    androidTestImplementation TestLibs.espresso_core
    androidTestImplementation TestLibs.espresso_contrib
    androidTestImplementation TestLibs.runner
    androidTestUtil TestLibs.orchestrator
}
repositories {
    mavenCentral()
}
