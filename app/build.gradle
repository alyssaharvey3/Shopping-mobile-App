plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.build_tools
    defaultConfig {
        applicationId "com.handstandsam.shoppingapp"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            resValue("string", "PORT_NUMBER", "8081") //Android Debug Database Configuration
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    flavorDimensions "server"

    productFlavors {
        inmemory {
            dimension "server"
        }

        mockserver {
            dimension "server"
        }

        liveserver {
            dimension "server"
        }
    }

    variantFilter { variant ->
        def fullName = variant.name.toLowerCase()
        def names = variant.flavors*.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (fullName.endsWith("debug")) {
            println(fullName + " - " + names)
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(false)
        } else {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "${compose_version}"
        kotlinCompilerVersion "${kotlin_version}"
    }

}

dependencies {

    implementation project(Modules.models)
    implementation project(Modules.mockData)

    //Support Libraries
    implementation Libs.androidx_appcompat_v7
    implementation Libs.androidx_recyclerview

    implementation Libs.androidx_lifecycle_runtime_ktx

    //Networking
    implementation Libs.glide
    implementation Libs.retrofit
    implementation Libs.okhttp

    //Kotlin
    implementation Libs.kotlin_std_lib
    implementation Libs.kotlin_coroutines
    implementation Libs.kotlin_coroutines_android

    implementation Libs.moshi

    //Flavor Modules
    inmemoryImplementation project(Modules.flavorInmemory)
    mockserverImplementation project(Modules.flavorMockserver)
    liveserverImplementation project(Modules.flavorLiveserver)

    //Logging
    implementation Libs.timber

    debugImplementation project(Modules.debug)


    // Network Inspection
    debugImplementation 'com.ashokvarma.android:gander-imdb:3.1.0'
    releaseImplementation 'com.ashokvarma.android:gander-no-op:3.1.0'

    implementation project(Modules.networking)
    implementation project(Modules.shoppingCart)
    implementation project(Modules.shoppingCartSqldelight)
    implementation project(Modules.shoppingCartRoom)

    // Compose
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.ui:ui-tooling:$compose_version"
    implementation "com.github.zsoltk:compose-router:0.17.0"
    implementation "dev.chrisbanes.accompanist:accompanist-coil:0.1.9"

    //JVM Tests Only
    testImplementation TestLibs.junit

    androidTestImplementation Libs.androidx_espresso
    androidTestImplementation Libs.androidx_espresso_contrib
    androidTestImplementation Libs.androidx_test_rules
    androidTestImplementation Libs.androidx_testrunner
    androidTestUtil Libs.androidx_test_orchestrator
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
    }
}

repositories {
    mavenCentral()
}
