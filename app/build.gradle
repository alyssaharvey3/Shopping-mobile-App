plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        applicationId "com.handstandsam.shoppingapp"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    flavorDimensions "server"

    productFlavors {
        inmemory {
            dimension "server"
        }

        mockserver {
            dimension "server"
        }

        liveserver {
            dimension "server"
        }
    }

    variantFilter { variant ->
        def fullName = variant.name.toLowerCase()
        def names = variant.flavors*.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (fullName.endsWith("debug")) {
            println(fullName + " - " + names)
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(false)
        } else {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerVersion kotlin_version
        kotlinCompilerExtensionVersion compose_version
    }
}

dependencies {

    implementation project(Modules.models)
    implementation project(Modules.mockData)

    //Support Libraries
    implementation Libs.androidx_appcompat_v7
    implementation Libs.androidx_recyclerview

    implementation Libs.androidx_lifecycle_runtime_ktx

    //Networking
    implementation Libs.glide
    implementation Libs.retrofit
    implementation Libs.okhttp

    //Kotlin
    implementation Libs.kotlin_std_lib
    implementation Libs.kotlin_coroutines
    implementation Libs.kotlin_coroutines_android

    implementation Libs.moshi

    //Flavor Modules
    inmemoryImplementation project(Modules.flavorInmemory)
    mockserverImplementation project(Modules.flavorMockserver)
    liveserverImplementation project(Modules.flavorLiveserver)

    //Logging
    implementation Libs.timber

    // Network Inspection
    debugImplementation 'com.ashokvarma.android:gander-imdb:3.1.0'
    releaseImplementation 'com.ashokvarma.android:gander-no-op:3.1.0'

    debugImplementation Libs.okhttp_logging_interceptor

    implementation project(Modules.networking)
    implementation project(Modules.shoppingCart)
//    implementation project(Modules.shoppingCartSqldelight)
//    implementation project(Modules.shoppingCartRoom)

    // Compose
    implementation "androidx.compose.compiler:compiler:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.activity:activity-compose:1.3.0"

    implementation "com.google.accompanist:accompanist-coil:0.15.0"
    implementation "io.coil-kt:coil-compose:1.3.1"
    implementation "com.github.skydoves:landscapist-coil:1.3.0"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha02'

    implementation 'com.google.android.material:material:1.4.0'

    //JVM Tests Only
    testImplementation TestLibs.junit

    androidTestImplementation Libs.androidx_espresso
    androidTestImplementation Libs.androidx_espresso_contrib
    androidTestImplementation Libs.androidx_test_rules
    androidTestImplementation Libs.androidx_testrunner
    androidTestUtil Libs.androidx_test_orchestrator
}

